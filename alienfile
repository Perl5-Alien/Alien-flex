use alienfile;

plugin 'Probe::GnuWin32' => (
  registery_key_regex => qr/^flex/i,
  exe_name => 'flex',
  exe_match => qr/flex/,
  exe_version => qr/version ([0-9\.]+)/,
);

plugin 'Probe::CommandLine' => (
  command => 'flex',
  args    => ['--version'],
  match   => qr/flex/,
  version => qr/([0-9\.]+)/,
);

share {

  requires 'Alien::bison' => '0.11';
  requires 'Alien::m4'    => '0.08';

  plugin 'Download' => (
    url     => 'https://github.com/westes/flex/files/981163/flex-2.6.4.tar.gz',
    version => qr/^flex-([0-9\.]+)\.tar\.gz$/,
  );

  plugin 'Extract' => 'tar.gz';  

  patch [ '%{patch} -p1 < %{.install.patch}/flex_2.6.4-6.2.diff' ];

  before build => sub {

    # this monstrosity is to ensure that the files have the correct
    # relative timestamps, otherwise the build will try to
    # regenerate things using autoconf, which is not wanted.
    requires 'Path::Tiny' => 0;
    for my $file ( 'configure.ac', 'aclocal.m4', 'Makefile.in', 'configure', 'doc/flex.1' ) {
        Path::Tiny::path($file)->touch;
	sleep(1);
    }

  };

  plugin 'Build::Autoconf' => ();

  gather sub {
    my($build) = @_;
    if(`flex --version` =~ /([0-9\.]+)/)
    {
      $build->runtime_prop->{version} = $1;
    }
    else
    {
      $build->runtime_prop->{version} ||= 'unknown';
    }
  };

}

